use text_size::{TextRange, TextSize};
use codespan_reporting::diagnostic::Diagnostic;

use crate::surface::{Const, Expr, Located, Pat, Token, TokenKind, FunParam, FunArg, Rec, ExprField, TypeField, PatField};
use crate::surface::LalrpopError;
use crate::plicity::Plicity;
use crate::symbol::Symbol;

grammar<'surface, 'text>(
    bump: &'surface bumpalo::Bump,
    text: &'text str,
    errors: &mut Vec<LalrpopError>,
);

extern {
    type Location = TextSize;
    type Error = std::convert::Infallible;

    enum TokenKind {
        "else" => TokenKind::KwElse,
        "false" => TokenKind::KwFalse,
        "forall" => TokenKind::KwForall,
        "fun" => TokenKind::KwFun,
        "if" => TokenKind::KwIf,
        "let" => TokenKind::KwLet,
        "rec" => TokenKind::KwRec,
        "then" => TokenKind::KwThen,
        "true" => TokenKind::KwTrue,

        "(" => TokenKind::LParen,
        ")" => TokenKind::RParen,
        "{" => TokenKind::LCurly,
        "}" => TokenKind::RCurly,
        "[" => TokenKind::LSquare,
        "]" => TokenKind::RSquare,

        "_" => TokenKind::Underscore,
        "," => TokenKind::Comma,
        ";" => TokenKind::Semicolon,
        ":" => TokenKind::Colon,
        "." => TokenKind::Dot,
        "@" => TokenKind::At,
        "=" => TokenKind::Eq,
        "=>" => TokenKind::DoubleArrow,
        "->" => TokenKind::SingleArrow,

        "Ident" => TokenKind::Ident,
        "DecInt" => TokenKind::DecInt,
        "BinInt" => TokenKind::BinInt,
        "HexInt" => TokenKind::HexInt,
    }
}

pub Expr: Located<Expr<'surface>> = {
    LetExpr => <>,
}

LetExpr: Located<Expr<'surface>> = {
    <start: @L> "let" <rec: Rec> <pat: Pat> <r#type: (":" <Expr>)?> "=" <init: Expr> ";" <body: Expr> <end: @R> => {
        let range = TextRange::new(start, end);
        let r#type = r#type.map(|r#type| bump.alloc(r#type) as &_);
        let (pat, init, body) = bump.alloc((pat, init, body));
        Located::new(range, Expr::Let { rec, pat, r#type, init, body })
    },
    <start: @L> "if" <cond: AppExpr> "then" <then: Expr> "else" <r#else: Expr> <end: @R> => {
        let range = TextRange::new(start, end);
        let (cond, then, r#else) = bump.alloc((cond, then, r#else));
        Located::new(range, Expr::If { cond, then, r#else })
    },
    <start: @L> <expr: AtomExpr> ":" <r#type: FunExpr> <end:@R> => {
        let range = TextRange::new(start, end);
        let (expr, r#type) = bump.alloc((expr, r#type));
        Located::new(range, Expr::Ann { expr, r#type })
    },
    FunExpr => <>,
}

Rec: Rec = {
    () => Rec::Nonrec,
    "rec" => Rec::Rec,
}

FunExpr: Located<Expr<'surface>> = {
    <start: @L> "fun" <params: FunParam+> "=>" <body: Expr> <end: @R> => {
        let range = TextRange::new(start, end);
        let params = bump.alloc_slice_copy(&params);
        let body = bump.alloc(body);
        Located::new(range, Expr::FunLit { params, body })
    },
    <start: @L> "forall" <params: FunParam+> "->" <body: Expr> <end: @R> => {
        let range = TextRange::new(start, end);
        let params = bump.alloc_slice_copy(&params);
        let body = bump.alloc(body);
        Located::new(range, Expr::FunType { params, body })
    },
    <start: @L> <plicity: Plicity> <lhs: AppExpr> "->" <rhs: FunExpr> <end: @R> => {
        let range = TextRange::new(start, end);
        let (lhs, rhs) = bump.alloc((lhs, rhs));
        Located::new(range, Expr::FunArrow { plicity, lhs, rhs })
    },
    AppExpr => <>,
}

FunParam: Located<FunParam<'surface>> = {
    <start: @L> "(" <plicity: Plicity> <pat: Pat> ":" <r#type: Expr> ")" <end: @R> => {
        let range = TextRange::new(start, end);
        Located::new(range, FunParam { plicity, pat, r#type: Some(r#type) })
    },
    <start: @L> <plicity: Plicity> <pat: Pat> <end: @R> => {
        let range = TextRange::new(start, end);
        Located::new(range, FunParam { plicity, pat, r#type: None })
    }
}

AppExpr: Located<Expr<'surface>> = {
    <start: @L> <fun: AppExpr> <arg: FunArg> <end: @R> => {
        let range = TextRange::new(start, end);
        let (fun, ) = bump.alloc((fun, ));
        Located::new(range, Expr::FunApp { fun, arg })
    },
    ProjExpr => <>,
}

ProjExpr: Located<Expr<'surface>> = {
    <start: @L> <scrut: ProjExpr> "." <name: Ident> <end: @R> => {
        let range = TextRange::new(start, end);
        let (scrut, ) = bump.alloc((scrut, ));
        Located::new(range, Expr::RecordProj { scrut, name })
    },
    AtomExpr => <>,
}

FunArg: Located<FunArg<'surface>> = {
    <start: @L> <plicity: Plicity> <expr: AtomExpr> <end: @R> => {
        let range = TextRange::new(start, end);
        let expr = bump.alloc(expr);
        Located::new(range, FunArg { plicity, expr })
    }
}

#[inline]
Plicity: Plicity = {
    "@" => Plicity::Implicit,
    ()  => Plicity::Explicit,
}

AtomExpr: Located<Expr<'surface>> = {
    <start: @L> <recovery: !> <end: @R> => {
        let range = TextRange::new(start, end);
        errors.push(recovery.error);
        Located::new(range, Expr::Error)
    },
    <start: @L> <r#const: Const> <end: @R> => Located::new(TextRange::new(start, end), Expr::Const(r#const)),
    <start: @L> <ident: Ident> <end: @R> => Located::new(TextRange::new(start, end), Expr::LocalVar(ident)),
    <start: @L> "_" <end: @R> => Located::new(TextRange::new(start, end), Expr::Hole),
    <start: @L> "(" <expr: Expr> ")" <end: @R> => Located::new(TextRange::new(start, end), Expr::Paren(bump.alloc(expr))),
    <start: @L> <exprs: Tuple<Expr>> <end: @R> => Located::new(TextRange::new(start, end), Expr::TupleLit(exprs)),
    <start: @L> "{" "}" <end: @R> => Located::new(TextRange::new(start, end), Expr::TupleLit(&[])),
    <start: @L> "{" <fields: Sep1<TypeField, ",">> "}" <end: @R> => Located::new(TextRange::new(start, end), Expr::RecordType(fields)),
    <start: @L> "{" <fields: Sep1<ExprField, ",">> "}" <end: @R> => Located::new(TextRange::new(start, end), Expr::RecordLit(fields)),
    <start: @L> "[" <exprs: Sep0<Expr, ",">> "]" <end: @R> => Located::new(TextRange::new(start, end), Expr::ListLit(exprs)),
}

Tuple<Elem>: &'surface [Elem] = {
    "(" ")" => &[],
    "(" <elem: Elem> "," ")" => bump.alloc_slice_copy(&[elem]),
    "(" <elems: Sep2<Elem, ",">> ")" => elems,
}

TypeField: Located<TypeField<'surface>> = {
    <start: @L> <name: Ident> ":" <r#type: Expr> <end: @R> => Located::new(TextRange::new(start, end), TypeField { name, r#type }),
}

ExprField: Located<ExprField<'surface>> = {
    <start: @L> <name: Ident> "=" <expr: Expr> <end: @R> => Located::new(TextRange::new(start, end), ExprField { name, expr }),
}

Pat: Located<Pat<'surface>> = {
    <start: @L> <recovery: !> <end: @R> => {
        let range = TextRange::new(start, end);
        errors.push(recovery.error);
        Located::new(range, Pat::Error)
    },
    <start: @L> "_" <end: @R> => Located::new(TextRange::new(start, end), Pat::Underscore),
    <start: @L> <ident: Ident> <end: @R> => Located::new(TextRange::new(start, end), Pat::Ident(ident)),
    <start: @L> "(" <pat: Pat> ")" <end: @R> => Located::new(TextRange::new(start, end), Pat::Paren(bump.alloc(pat))),
    <start: @L> <pats: Tuple<Pat>> <end: @R> => Located::new(TextRange::new(start, end), Pat::TupleLit(pats)),
    <start: @L> "{" <fields: Sep0<PatField, ",">> "}" <end: @R> => Located::new(TextRange::new(start, end), Pat::RecordLit(fields)),
}

PatField: Located<PatField<'surface>> = {
    <start: @L> <name: Ident> "=" <pat: Pat> <end: @R> => Located::new(TextRange::new(start, end), PatField { name, pat }),
}

Const: Const = {
    "true" => Const::Bool(true),
    "false" => Const::Bool(false),
    "DecInt" => Const::DecInt,
    "BinInt" => Const::BinInt,
    "HexInt" => Const::HexInt,
}

Ident: Located<Symbol> = {
    <start: @L> "Ident" <end: @R> => {
        let range = TextRange::new(start, end);
        let text = &text[range];
        Located::new(range, Symbol::intern(text))
    }
}

Sep0<Elem, Sep>: &'surface [Elem] = {
    <mut elems: (<Elem> Sep)*> <last: Elem?> => {
        elems.extend(last);
        bump.alloc_slice_copy(&elems)
    }
};

Sep1<Elem, Sep>: &'surface [Elem] = {
    <mut elems: (<Elem> Sep)*> <last: Elem> Sep? => {
        elems.push(last);
        bump.alloc_slice_copy(&elems)
    }
};

Sep2<Elem, Sep>: &'surface [Elem] = {
    <mut elems: (<Elem> Sep)+> <last: Elem> Sep? => {
        elems.push(last);
        bump.alloc_slice_copy(&elems)
    }
};
